/mob/living/carbon/human/var/cpr_cooldown
/mob/living/carbon/human/var/cpr_attempt_timer
/mob/living/carbon/human/attack_hand(mob/living/carbon/human/attacking_mob, list/mods)
	if(..())
		return TRUE

	if(HAS_TRAIT(attacking_mob, TRAIT_HAULED))
		return

	SEND_SIGNAL(attacking_mob, COMSIG_LIVING_ATTACKHAND_HUMAN, src)

	if((attacking_mob != src) && check_shields(attacking_mob.name, get_dir(src, attacking_mob), custom_response = TRUE))
		visible_message(SPAN_DANGER("<B>[attacking_mob] attempted to touch [src]!</B>"), null, null, 5)
		return FALSE

	var/obj/limb/targetted_limb = get_limb(attacking_mob.zone_selected)
	var/is_bleeding = FALSE
	if(targetted_limb)
		for(var/datum/effects/bleeding/blood in targetted_limb.bleeding_effects_list)
			if(istype(blood, /datum/effects/bleeding/external) || istype(blood, /datum/effects/bleeding/arterial))
				is_bleeding = TRUE
				break

	switch(attacking_mob.a_intent)
		if(INTENT_HELP)

			if(on_fire && attacking_mob)
				if(attacking_mob == src)
					adjust_fire_stacks(-2, min_stacks = 0)
					playsound(src.loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
					attacking_mob.visible_message(SPAN_DANGER("[attacking_mob] tries to put out the fire on themselves!"),
						SPAN_WARNING("You try to put out the fire on yourself!"), null, 5)
					if(fire_stacks <= 0)
						attacking_mob.visible_message(SPAN_DANGER("[attacking_mob] has successfully extinguished the fire on themselves!"),
							SPAN_NOTICE("You extinguished the fire on yourself, phew."), null, 5)
				else
					adjust_fire_stacks(-4, min_stacks = 0) // reduced since literally having someone pat you out is better than actually rolling
					playsound(src.loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
					attacking_mob.visible_message(SPAN_DANGER("[attacking_mob] tries to put out the fire on [src]!"),
						SPAN_WARNING("You try to put out the fire on [src]!"), null, 5)

					if(fire_stacks <= 0)
						attacking_mob.visible_message(SPAN_DANGER("[attacking_mob] has successfully extinguished the fire on [src]!"),
							SPAN_NOTICE("You extinguished the fire on [src]."), null, 5)

				var/hand_to_burn = attacking_mob.get_active_hand()
				var/damage_dealt = attacking_mob.apply_armoured_damage(rand(3, 10), ARMOR_BIO, BURN, hand_to_burn) // probably better to calculate the damage based on flame intensity but this may work for now
				var/hand_string = attacking_mob.hand ? "left hand" : "right hand"
				if(damage_dealt > 0)
					to_chat(attacking_mob, SPAN_WARNING("You burn your [hand_string] trying to put out the flames!"))

				return TRUE

			if(targetted_limb && stat == CONSCIOUS && is_bleeding)
				if(attacking_mob.action_busy)
					return FALSE

				var/self = (attacking_mob == src)
				attacking_mob.visible_message(SPAN_NOTICE("[attacking_mob] starts applying pressure to [self ? "their" : "[src]'s"] [targetted_limb.display_name]."),
					SPAN_HELPFUL("You start applying pressure to [self ? "your" : "[src]'s"] [targetted_limb.display_name]..."))

				var/do_after_result = FALSE
				if(self)
					do_after_result = do_after(src, 3 SECONDS, (INTERRUPT_ALL & (~INTERRUPT_MOVED)), BUSY_ICON_MEDICAL, status_effect = SLOW)
				else
					do_after_result = do_after(src, 3 SECONDS, (INTERRUPT_ALL & (~INTERRUPT_MOVED)), BUSY_ICON_FRIENDLY, attacking_mob, INTERRUPT_ALL, BUSY_ICON_MEDICAL)

				if(do_after_result)

					var/stopped_bleeding = FALSE
					var/severe_bleeding = FALSE
					var/cannot_reduce_further = FALSE
					for(var/datum/effects/bleeding/blood in targetted_limb.bleeding_effects_list)
						if(blood.blood_loss >= 1)
							if(blood.blood_loss == 1)
								cannot_reduce_further = TRUE
							else
								blood.blood_loss = max(1, blood.blood_loss - 0.05)
								severe_bleeding = TRUE
						else
							blood.blood_loss = max(0, blood.blood_loss - 0.05)
							if(blood.blood_loss == 0)
								stopped_bleeding = TRUE

					if(stopped_bleeding)
						attacking_mob.visible_message(SPAN_NOTICE("[attacking_mob] applies pressure to [self ? "their" : "[src]'s"] [targetted_limb.display_name], stopping the bleeding."),
							SPAN_HELPFUL("You apply pressure to [self ? "your" : "[src]'s"] [targetted_limb.display_name], stopping the bleeding!"))
					else if(cannot_reduce_further)
						attacking_mob.visible_message(SPAN_NOTICE("[attacking_mob] applies pressure to [self ? "their" : "[src]'s"] [targetted_limb.display_name], trying to stop the bleeding."),
							SPAN_HELPFUL("You continue to apply pressure to [self ? "your" : "[src]'s"] [targetted_limb.display_name], but can't reduce the bleeding any further by hand."))
					else if(targetted_limb && severe_bleeding)
						attacking_mob.visible_message(SPAN_NOTICE("[attacking_mob] applies pressure to [self ? "their" : "[src]'s"] [targetted_limb.display_name], trying to stop the bleeding."),
							SPAN_HELPFUL("You apply pressure to [self ? "your" : "[src]'s"] [targetted_limb.display_name], although you slow the bleeding, it is too severe to stop by hand!"))
					else
						attacking_mob.visible_message(SPAN_NOTICE("[attacking_mob] applies pressure to [self ? "their" : "[src]'s"] [targetted_limb.display_name], slowing the bleeding."),
							SPAN_HELPFUL("You apply pressure to [self ? "your" : "[src]'s"] [targetted_limb.display_name], slowing the bleeding."))

					playsound(src.loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 5)
					attacking_mob.add_blood(get_blood_color(), BLOOD_HANDS)
				return TRUE

			// If unconscious with oxygen damage, do CPR. If dead, we do CPR
			if(!(stat == UNCONSCIOUS && getOxyLoss() > 0) && !(stat == DEAD))
				help_shake_act(attacking_mob)
				return 1

			if(species.flags & IS_SYNTHETIC)
				to_chat(attacking_mob, SPAN_DANGER("Your hands compress the metal chest uselessly... "))
				return 0

			if(cpr_attempt_timer >= world.time)
				to_chat(attacking_mob, SPAN_NOTICE("<B>CPR is already being performed on [src]!</B>"))
				return 0

			//CPR
			if(attacking_mob.action_busy)
				return 1

			attacking_mob.visible_message(SPAN_NOTICE("<b>[attacking_mob]</b> starts performing <b>CPR</b> on <b>[src]</b>."),
				SPAN_HELPFUL("You start <b>performing CPR</b> on <b>[src]</b>."))

			cpr_attempt_timer = world.time + HUMAN_STRIP_DELAY * attacking_mob.get_skill_duration_multiplier(SKILL_MEDICAL)
			if(do_after(attacking_mob, HUMAN_STRIP_DELAY * attacking_mob.get_skill_duration_multiplier(SKILL_MEDICAL), INTERRUPT_ALL, BUSY_ICON_GENERIC, src, INTERRUPT_MOVED, BUSY_ICON_MEDICAL))
				if(stat != DEAD)
					var/suff = min(getOxyLoss(), 10) //Pre-merge level, less healing, more prevention of dieing.
					apply_damage(-suff, OXY)
					updatehealth()
					src.affected_message(attacking_mob,
						SPAN_HELPFUL("You feel a <b>breath of fresh air</b> enter your lungs. It feels good."),
						SPAN_HELPFUL("You <b>perform CPR</b> on <b>[src]</b>. Repeat at least every <b>7 seconds</b>."),
						SPAN_NOTICE("<b>[attacking_mob]</b> performs <b>CPR</b> on <b>[src]</b>."))
				if(is_revivable() && stat == DEAD)
					if(cpr_cooldown < world.time)
						revive_grace_period += 7 SECONDS
						attacking_mob.visible_message(SPAN_NOTICE("<b>[attacking_mob]</b> performs <b>CPR</b> on <b>[src]</b>."),
							SPAN_HELPFUL("You perform <b>CPR</b> on <b>[src]</b>."))
						balloon_alert(attacking_mob, "you perform cpr")
					else
						attacking_mob.visible_message(SPAN_NOTICE("<b>[attacking_mob]</b> fails to perform CPR on <b>[src]</b>."),
							SPAN_HELPFUL("You <b>fail</b> to perform <b>CPR</b> on <b>[src]</b>. Incorrect rhythm. Do it <b>slower</b>."))
						balloon_alert(attacking_mob, "incorrect rhythm, do it slower")
					cpr_cooldown = world.time + 7 SECONDS
				cpr_attempt_timer = 0
				return 1

		if(INTENT_GRAB)
			if(attacking_mob == src)
				check_for_injuries(src)
				return 1

			if(anchored)
				return 0

			if(w_uniform)
				w_uniform.add_fingerprint(attacking_mob)

			attacking_mob.start_pulling(src)
			return 1

		if(INTENT_HARM)
			// See if they can attack, and which attacks to use.
			var/datum/unarmed_attack/attack = attacking_mob.species.unarmed
			if(!attack.is_usable(attacking_mob))
				attack = attacking_mob.species.secondary_unarmed
				return

			last_damage_data = create_cause_data("fisticuffs", attacking_mob)
			attacking_mob.attack_log += text("\[[time_stamp()]\] <font color='red'>[pick(attack.attack_verb)]ed [key_name(src)]</font>")
			attack_log += text("\[[time_stamp()]\] <font color='orange'>Has been [pick(attack.attack_verb)]ed by [key_name(attacking_mob)]</font>")
			msg_admin_attack("[key_name(attacking_mob)] [pick(attack.attack_verb)]ed [key_name(src)] in [get_area(src)] ([src.loc.x],[src.loc.y],[src.loc.z]).", src.loc.x, src.loc.y, src.loc.z)

			attacking_mob.animation_attack_on(src)
			attacking_mob.flick_attack_overlay(src, "punch")

			var/extra_cqc_dmg = 0 //soft maximum of 5, this damage is added onto the final value depending on how much cqc skill you have
			if(attacking_mob.skills)
				extra_cqc_dmg = attacking_mob.skills?.get_skill_level(SKILL_CQC)
			var/raw_damage = 0 //final value, gets absorbed by the armor and then deals the leftover to the mob

			var/obj/limb/affecting = get_limb(rand_zone(attacking_mob.zone_selected, 70))
			var/armor = getarmor(affecting, ARMOR_MELEE)

			playsound(loc, attack.attack_sound, 25, 1)

			visible_message(SPAN_DANGER("[attacking_mob] [pick(attack.attack_verb)]ed [src]!"), null, null, 5)

			raw_damage = attack.damage + extra_cqc_dmg
			var/final_damage = armor_damage_reduction(GLOB.marine_melee, raw_damage, armor, FALSE) // no penetration from punches
			apply_damage(final_damage, BRUTE, affecting, sharp=attack.sharp, edge = attack.edge)

		if(INTENT_DISARM)
			if(attacking_mob == src)
				check_for_injuries(src)
				return 1

			attacking_mob.attack_log += text("\[[time_stamp()]\] <font color='red'>Disarmed [key_name(src)]</font>")
			src.attack_log += text("\[[time_stamp()]\] <font color='orange'>Has been disarmed by [key_name(attacking_mob)]</font>")

			attacking_mob.animation_attack_on(src)
			attacking_mob.flick_attack_overlay(src, "disarm")

			msg_admin_attack("[key_name(attacking_mob)] disarmed [key_name(src)] in [get_area(src)] ([src.loc.x],[src.loc.y],[src.loc.z]).", src.loc.x, src.loc.y, src.loc.z)

			if(w_uniform)
				w_uniform.add_fingerprint(attacking_mob)

			//Accidental gun discharge
			if(!skillcheck(attacking_mob, SKILL_CQC, SKILL_CQC_SKILLED))
				if (isgun(r_hand) || isgun(l_hand))
					var/obj/item/weapon/gun/held_weapon = null
					var/chance = 0

					if (isgun(l_hand))
						held_weapon = l_hand
						chance = hand ? 40 : 20

					if (isgun(r_hand))
						held_weapon = r_hand
						chance = !hand ? 40 : 20

					if (prob(chance))
						visible_message(SPAN_DANGER("[attacking_mob] accidentally discharges [src]'s [held_weapon.name] during the struggle!"), SPAN_DANGER("[attacking_mob] accidentally discharges your [held_weapon.name] during the struggle!"), null, 5)
						var/list/turfs = list()
						for(var/turf/turfs_to_discharge in view())
							turfs += turfs_to_discharge
						var/turf/target = pick(turfs)
						count_niche_stat(STATISTICS_NICHE_DISCHARGE)
						held_weapon.Fire(target, src)

						attack_log += "\[[time_stamp()]\] <b>[key_name(src)]</b> accidentally discharged <b>[held_weapon.name]</b> in [get_area(src)] triggered by <b>[key_name(attacking_mob)]</b>."
						attacking_mob.attack_log += "\[[time_stamp()]\] <b>[key_name(src)]</b> accidentally fired <b>[held_weapon.name]</b> in [get_area(src)] triggered by <b>[key_name(attacking_mob)]</b>."
						msg_admin_ff("[key_name(src)][ADMIN_JMP(src)] [ADMIN_PM(src)] accidentally discharged <b>[held_weapon.name]</b> in [get_area(src)] ([src.loc.x],[src.loc.y],[src.loc.z]) triggered by <b>[key_name(attacking_mob)][ADMIN_JMP(attacking_mob)] [ADMIN_PM(attacking_mob)]</b>.")

			var/disarm_chance = rand(1, 100)
			var/attacker_skill_level = attacking_mob.skills ? attacking_mob.skills.get_skill_level(SKILL_CQC) : SKILL_CQC_MAX // No skills, so assume max
			var/defender_skill_level = skills ? skills.get_skill_level(SKILL_CQC) : SKILL_CQC_MAX // No skills, so assume max
			disarm_chance -= 5 * attacker_skill_level
			disarm_chance += 5 * defender_skill_level

			if(disarm_chance <= 25)
				var/strength = 2 + max((attacker_skill_level - defender_skill_level), 0)
				KnockDown(strength)
				Stun(strength)
				playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
				var/shove_text = attacker_skill_level > 1 ? "tackled" : pick("pushed", "shoved")
				visible_message(SPAN_DANGER("<B>[attacking_mob] has [shove_text] [src]!</B>"), null, null, 5)
				return

			if(disarm_chance <= 60)
				//BubbleWrap: Disarming breaks a pull
				if(pulling)
					visible_message(SPAN_DANGER("<b>[attacking_mob] has broken [src]'s grip on [pulling]!</B>"), null, null, 5)
					stop_pulling()
				else
					drop_held_item()
					visible_message(SPAN_DANGER("<B>[attacking_mob] has disarmed [src]!</B>"), null, null, 5)
				playsound(loc, 'sound/weapons/punchmiss.ogg', 25, 1, 7)
				return

			playsound(loc, 'sound/weapons/punchmiss.ogg', 25, 1, 7)
			visible_message(SPAN_DANGER("<B>[attacking_mob] attempted to disarm [src]!</B>"), null, null, 5)

/mob/living/carbon/human/proc/afterattack(atom/target as mob|obj|turf|area, mob/living/user as mob|obj, inrange, params)
	return

/mob/living/carbon/human/help_shake_act(mob/living/carbon/M)
	//Target is us
	if(src == M)
		check_for_injuries(src)
		return

	//Target is not us
	var/t_him = "it"
	switch(gender)
		if(MALE)
			t_him = "him"
		if(FEMALE)
			t_him = "her"
		if(PLURAL)
			t_him = "them"

	if (w_uniform)
		w_uniform.add_fingerprint(M)

	if(HAS_TRAIT(src, TRAIT_FLOORED) || HAS_TRAIT(src, TRAIT_KNOCKEDOUT) || body_position == LYING_DOWN || sleeping)
		if(client)
			sleeping = max(0,src.sleeping-5)
		if(!sleeping)
			if(is_dizzy)
				to_chat(M, SPAN_WARNING("[src] looks dizzy. Maybe you should let [t_him] rest a bit longer."))
			else
				set_resting(FALSE)
		M.visible_message(SPAN_NOTICE("[M] shakes [src] trying to wake [t_him] up!"),
			SPAN_NOTICE("You shake [src] trying to wake [t_him] up!"), null, 4)
	else if(HAS_TRAIT(src, TRAIT_INCAPACITATED))
		M.visible_message(SPAN_NOTICE("[M] shakes [src], trying to shake [t_him] out of his stupor!"),
			SPAN_NOTICE("You shake [src], trying to shake [t_him] out of his stupor!"), null, 4)
	else
		var/mob/living/carbon/human/H = M
		if(istype(H))
			H.species.hug(H, src, H.zone_selected)
		else
			M.visible_message(SPAN_NOTICE("[M] pats [src] on the back to make [t_him] feel better!"),
				SPAN_NOTICE("You pat [src] on the back to make [t_him] feel better!"), null, 4)
			playsound(src.loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 5)
		return

	adjust_effect(-6, PARALYZE)
	adjust_effect(-6, STUN)
	adjust_effect(-6, WEAKEN)

	playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)

/mob/living/carbon/human/proc/check_for_injuries(mob/living/carbon/human/checker)
	if(!checker)
		return

	if(checker == src) // this probably would be better off in a separate, universal proc so itll be easier for everyone involved i think
		var/pronoun = "itself"
		switch(gender)
			if(MALE)
				pronoun = "himself"
			if(FEMALE)
				pronoun = "herself"
			if(PLURAL)
				pronoun = "themself"
		visible_message(SPAN_NOTICE("[src] examines [pronoun] for injuries."),
			SPAN_NOTICE("You check yourself for injuries."), null, 3)
	else
		checker.visible_message(SPAN_NOTICE("[checker] examines [src]."), SPAN_NOTICE("You check [src] for injuries."), null, 3)

	var/list/limb_message = list()
	for(var/obj/limb/org as anything in limbs)
		var/list/status = list()
		var/brutedamage = org.brute_dam
		var/burndamage = org.burn_dam
		if(org.status & LIMB_DESTROYED)
			status += "MISSING!"
		else if(org.status & (LIMB_ROBOT|LIMB_SYNTHSKIN))
			switch(brutedamage)
				if(1 to 20)
					status += "dented"
				if(20 to 40)
					status += "battered"
				if(40 to INFINITY)
					status += "mangled"

			switch(burndamage)
				if(1 to 10)
					status += "singed"
				if(10 to 40)
					status += "scorched"
				if(40 to INFINITY)
					status += "charred"

		else
			if(org.status & LIMB_MUTATED)
				status += "weirdly shaped"
			if(halloss > 0)
				status += "tingling"
			switch(brutedamage)
				if(1 to 20)
					status += "bruised"
				if(20 to 40)
					status += "battered"
				if(40 to INFINITY)
					status += "mangled"

			switch(burndamage)
				if(1 to 10)
					status += "numb"
				if(10 to 40)
					status += "blistered"
				if(40 to INFINITY)
					status += "peeling away"

		if(org.get_incision_depth()) //Unindented because robotic and severed limbs may also have surgeries performed upon them.
			status += "cut open"

		for(var/datum/effects/bleeding/external/E in org.bleeding_effects_list)
			status += "bleeding"
			break

		var/limb_surgeries = org.get_active_limb_surgeries()
		if(limb_surgeries)
			status += "undergoing [limb_surgeries]"

		if(!length(status))
			status += "OK"

		var/postscript
		if(checker == src) // obviously it wont come off as obvious to other people, unless its dislocated or w/e
			if(org.status & LIMB_UNCALIBRATED_PROSTHETIC)
				postscript += " <b>(NONFUNCTIONAL)</b>"
			if(org.status & LIMB_BROKEN)
				postscript += " <b>(BROKEN)</b>"

		if(org.status & LIMB_SPLINTED_INDESTRUCTIBLE)
			postscript += " <b>(<a href='byond://?src=\ref[src];remove_splint=[org.name]'><span class='corp_label_red'>NANOSPLINTED</a>)</b>"
		else if(org.status & LIMB_SPLINTED)
			postscript += " <b>(<a href='byond://?src=\ref[src];remove_splint=[org.name]'><span class='corp_label_red'>SPLINTED</a>)</b>"

		for(var/datum/effects/bleeding/arterial/art_bleed in org.bleeding_effects_list)
			postscript += " <b>It is bleeding profusely.</b>"
		for(var/datum/effects/bleeding/internal/int_bleed in org.bleeding_effects_list)
			postscript += " <b>The skin looks to be discolored.</b>"

		if(org.status & LIMB_DISLOCATED)
			postscript += " <b>(DISLOCATED)</b>" //href this to undislocate obviously with tarkovmed integrity
		if(org.status & LIMB_CONSTRICTED)
			postscript += " <b>(<a href='byond://?src=\ref[src];remove_tourniquet=[org.name]'><span class='corp_label_red'>CONSTRICTED</a>)</b>"
			postscript = replacetext(postscript, " <b>The skin looks to be discolored.</b>", "")
			postscript = replacetext(postscript, " <b>It is bleeding profusely.</b>", "")
		if(org.status & LIMB_THIRD_DEGREE_BURNS)
			postscript += "<b>(SEVERE BURN)</b>"
		if(org.status & LIMB_ESCHAR)
			postscript += " <b>(ESCHAR)</b>"


		if(postscript)
			limb_message += "\t [checker == src ? "My" : "[src]'s"] [org.display_name] is [SPAN_WARNING("[english_list(status, final_comma_text = ",")].[postscript]")]"
		else
			limb_message += "\t [checker == src ? "My" : "[src]'s"] [org.display_name] is [status[1] == "OK" ? SPAN_NOTICE("OK.") : SPAN_WARNING("[english_list(status, final_comma_text = ",")].")]"
	limb_message += "\t \n<span class = 'deptradio'>Medical actions: <a href='byond://?src=\ref[src];check_status=1'>\[Check Status\]</a>\n"
	to_chat(checker, boxed_message(limb_message.Join("<br>")))
